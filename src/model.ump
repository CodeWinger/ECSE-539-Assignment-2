namespace ca.mcgill.ecse539.btms.model;

class BTMS{
 isA Serializable;
singleton;
1 <@>- * Bus;
1 <@>- * Route;
1 <@>- * Driver; 
1 <@>- * MorningRouteWorkShift;
1 <@>- * AfternoonRouteWorkShift;
1 <@>- * NightRouteWorkShift;
1 <@>- * DriverBusRouteTuple;
 
 lazy immutable Date currentDate;
 lazy immutable Date threeDaysAhead;
 
 /*WARNING: This before call doesn't work in the constructor BTMS, 
 instead the code written below has been directly hard-coded in the BTMS constructor.
 Future automatic generations of the model will not have this code written anywhere.
 See the report for more information regarding this issue.*/

 
 /*WARNING: currently in BTMS, there are 3 methods with each of them being overloaded once for
 a total of 6 methods. They are:
   1. MorningRouteWorkShift addMorningRouteWorkShift(Route aRoute, Date aWorkDate) and
   2. boolean addMorningRouteWorkShift(MorningRouteWorkShift aMorningRouteWorkShift)
  	  
   3. AfternoonRouteWorkShift addAfternoonRouteWorkShift(Route aRoute, Date aWorkDate) and
   4. boolean addAfternoonRouteWorkShift(AfternoonRouteWorkShift aAfternoonRouteWorkShift)
   	  
   5. NightRouteWorkShift addNightRouteWorkShift(Route aRoute, Date aWorkDate) and
   6. boolean addNightRouteWorkShift(NightRouteWorkShift aNightRouteWorkShift)
   
Unfortunately, we couldn't find a way to specify in which overloaded method we wanted to inject code
and to add insult to injury, by default, it generates the code in the method we didn't want (2,4,6)!
Therefore, the code written below has been directly hard-coded in methods (1,3,5) and future automatic 
generations of the model will not have these snippets of code written anymore. See the report for more 
information regarding this issue.*/   
    							
/*	before addMorningRouteWorkShift{
	 	if( !(aWorkDate.after(getCurrentDate()) && aWorkDate.before(getThreeDaysAhead())))
	 		return null;
	}
*/

 before constructor {
	Calendar calendar = Calendar.getInstance();
	setCurrentDate(new java.sql.Date(calendar.getTime().getTime()));
	calendar.add(Calendar.DATE, 3);
	setThreeDaysAhead(new java.sql.Date(calendar.getTime().getTime()));
	}
	

}

class Bus
{  
  isA Serializable;
  unique String licensePlate;
  busStatus {
  		FUNCTIONNAL {
  			busBreaksDown -> IN_REPAIR;
  		}
  		IN_REPAIR {
  			busRepaired -> FUNCTIONNAL;
  		}
  }
}

class Driver{
	isA Serializable;
   unique String name;
   autounique id;
  
  workStatus {
  		CAN_WORK {
  			driverStrickenWithIllness -> SICK;
  		} 		
  		SICK {
  			driverFeelsBetter -> CAN_WORK;
  		}
  }
}

class Route {
	isA Serializable;
	unique Integer routeNumber;
	[routeNumber > 0]
}

class RouteWorkShift {
 isA Serializable;
	abstract;
	1 -- * DriverBusRouteTuple;

}

class DriverBusRouteTuple {
 isA Serializable;
 	* -- 1 Driver;
 	* -- 1 Bus;
 	* -- 1 Route;
 	
 	before setBus{
		 if(aBus.getBusStatus() ==  ca.mcgill.ecse539.btms.model.Bus.BusStatus.IN_REPAIR)
    			return false;
	}
	
	before setDriver{
		if(aDriver.getWorkStatus() == ca.mcgill.ecse539.btms.model.Driver.WorkStatus.SICK)
    			return false;
	}
	
	before constructor{
		final Date date;
	    if (aRouteWorkShift instanceof MorningRouteWorkShift) {
	    	date = ((MorningRouteWorkShift) aRouteWorkShift).getWorkDate();
	    } else if ( aRouteWorkShift instanceof AfternoonRouteWorkShift) {
	    	date = ((AfternoonRouteWorkShift) aRouteWorkShift).getWorkDate();
	    } else if ( aRouteWorkShift instanceof NightRouteWorkShift){
	    	date = ((NightRouteWorkShift) aRouteWorkShift).getWorkDate();
	    } else {
	    	date = null;
	    }
	    
	    List<DriverBusRouteTuple> assignmentsForGivenDay = new ArrayList<DriverBusRouteTuple>();
	    aBTMS.getAfternoonRouteWorkShifts().stream().filter(s -> s.getWorkDate().equals(date)).map(s -> s.getDriverBusRouteTuples()).forEach(assignmentsForGivenDay::addAll);
	    aBTMS.getMorningRouteWorkShifts().stream().filter(s -> s.getWorkDate().equals(date)).map(s -> s.getDriverBusRouteTuples()).forEach(assignmentsForGivenDay::addAll);
	    aBTMS.getNightRouteWorkShifts().stream().filter(s -> s.getWorkDate().equals(date)).map(s -> s.getDriverBusRouteTuples()).forEach(assignmentsForGivenDay::addAll);
	    
	    if ( assignmentsForGivenDay.stream().anyMatch(a -> a.getRoute().getRouteNumber() != aRoute.getRouteNumber())) {
	    	throw new RuntimeException("cannot assign a bus to different route for the same day!");
	    }
	}
}

class MorningRouteWorkShift{
 isA Serializable;
	isA RouteWorkShift;
	
	unique Date workDate;
	immutable shiftName = "Morning";
}

class AfternoonRouteWorkShift{
 isA Serializable;
	isA RouteWorkShift;
	
	unique Date workDate;
	immutable shiftName = "Afternoon";
}

class NightRouteWorkShift{
 isA Serializable;
	isA RouteWorkShift;
	
	unique Date workDate;
	immutable shiftName = "Night";
}

interface Serializable {
}